# Network creation
resource "openstack_networking_network_v2" "network_v6" {
  name           = "${var.network_prefix}-v6"
  admin_state_up = "true"
}

# Subnet creation
resource "openstack_networking_subnet_v2" "subnet_v6" {
  name              = "subnet_v6"
  network_id        = openstack_networking_network_v2.network_v6.id
  cidr              = "2001:cafe::/64"
  ip_version        = 6
  ipv6_address_mode = "dhcpv6-stateful"
  ipv6_ra_mode      = "dhcpv6-stateful"
}

# Port creation
resource "openstack_networking_port_v2" "lb_port_v6" {
  name               = "${var.network_prefix}-lb-port-v6"
  network_id         = openstack_networking_network_v2.network_v6.id
  security_group_ids = [openstack_networking_secgroup_v2.external.id]
  admin_state_up     = "true"

  fixed_ip {
    subnet_id = openstack_networking_subnet_v2.subnet_v6.id
  }

  allowed_address_pairs {
    ip_address = var.allowed_address_pairs_cidr
  }
}

resource "openstack_networking_port_v2" "port_v6" {
  for_each           = setunion(var.master_node_names, var.worker_node_names)
  name               = "${var.network_prefix}-port-v6"
  network_id         = openstack_networking_network_v2.network_v6.id
  security_group_ids = [openstack_networking_secgroup_v2.internal.id]
  admin_state_up     = "true"

  fixed_ip {
    subnet_id = openstack_networking_subnet_v2.subnet_v6.id
  }

  allowed_address_pairs {
    ip_address = var.allowed_address_pairs_cidr
  }
}

# Router creation
resource "openstack_networking_router_v2" "generic_v6" {
  name = "router-generic_v6"
}

# Router interface creation
resource "openstack_networking_router_interface_v2" "router_interface_v6" {
  router_id = openstack_networking_router_v2.generic_v6.id
  subnet_id = openstack_networking_subnet_v2.subnet_v6.id
}

# rules creation
resource "openstack_networking_secgroup_rule_v2" "ext_v6_rules" {
  for_each          = var.ext_ports
  description       = each.key
  direction         = "ingress"
  ethertype         = "IPv6"
  protocol          = each.value.protocol
  port_range_min    = each.value.min
  port_range_max    = each.value.max
  remote_ip_prefix  = "::/0"
  security_group_id = openstack_networking_secgroup_v2.external.id
}

resource "openstack_networking_secgroup_rule_v2" "ext_to_int_v6" {
  for_each          = var.ext_ports
  description       = each.key
  direction         = "ingress"
  ethertype         = "IPv6"
  protocol          = each.value.protocol
  port_range_min    = each.value.min
  port_range_max    = each.value.max
  remote_group_id   = openstack_networking_secgroup_v2.external.id
  security_group_id = openstack_networking_secgroup_v2.internal.id
}

resource "openstack_networking_secgroup_rule_v2" "int_v6" {
  for_each          = var.int_ports
  description       = each.key
  direction         = "ingress"
  ethertype         = "IPv6"
  protocol          = each.value.protocol
  port_range_min    = each.value.min
  port_range_max    = each.value.max
  remote_group_id   = openstack_networking_secgroup_v2.internal.id
  security_group_id = openstack_networking_secgroup_v2.internal.id
}
